{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "--- Day 4: Passport Processing ---\n",
    "You arrive at the airport only to realize that you grabbed your North Pole Credentials instead of your passport. While these documents are extremely similar, North Pole Credentials aren't issued by a country and therefore aren't actually valid documentation for travel in most of the world.\n",
    "\n",
    "It seems like you're not the only one having problems, though; a very long line has formed for the automatic passport scanners, and the delay could upset your travel itinerary.\n",
    "\n",
    "Due to some questionable network security, you realize you might be able to solve both of these problems at the same time.\n",
    "\n",
    "The automatic passport scanners are slow because they're having trouble detecting which passports have all required fields. The expected fields are as follows:\n",
    "\n",
    "byr (Birth Year)\n",
    "iyr (Issue Year)\n",
    "eyr (Expiration Year)\n",
    "hgt (Height)\n",
    "hcl (Hair Color)\n",
    "ecl (Eye Color)\n",
    "pid (Passport ID)\n",
    "cid (Country ID)\n",
    "Passport data is validated in batch files (your puzzle input). Each passport is represented as a sequence of key:value pairs separated by spaces or newlines. Passports are separated by blank lines.\n",
    "\n",
    "Here is an example batch file containing four passports:\n",
    "\n",
    "ecl:gry pid:860033327 eyr:2020 hcl:#fffffd\n",
    "byr:1937 iyr:2017 cid:147 hgt:183cm\n",
    "\n",
    "iyr:2013 ecl:amb cid:350 eyr:2023 pid:028048884\n",
    "hcl:#cfa07d byr:1929\n",
    "\n",
    "hcl:#ae17e1 iyr:2013\n",
    "eyr:2024\n",
    "ecl:brn pid:760753108 byr:1931\n",
    "hgt:179cm\n",
    "\n",
    "hcl:#cfa07d eyr:2025 pid:166559648\n",
    "iyr:2011 ecl:brn hgt:59in\n",
    "The first passport is valid - all eight fields are present. The second passport is invalid - it is missing hgt (the Height field).\n",
    "\n",
    "The third passport is interesting; the only missing field is cid, so it looks like data from North Pole Credentials, not a passport at all! Surely, nobody would mind if you made the system temporarily ignore missing cid fields. Treat this \"passport\" as valid.\n",
    "\n",
    "The fourth passport is missing two fields, cid and byr. Missing cid is fine, but missing any other field is not, so this passport is invalid.\n",
    "\n",
    "According to the above rules, your improved system would report 2 valid passports.\n",
    "\n",
    "Count the number of valid passports - those that have all required fields. Treat cid as optional. In your batch file, how many passports are valid?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "let input = System.IO.File.ReadAllLines \"./input.txt\" |> List.ofArray\n",
    "\n",
    "let passportStrings =\n",
    "    List.fold (fun (acc : string list list) (s : string) ->\n",
    "        match (acc, s) with\n",
    "        | [], s -> [[s]]\n",
    "        | xs, \"\" -> [] :: xs\n",
    "        | x :: xs, s -> (s :: x) :: xs\n",
    "    ) [] input\n",
    "    |> List.rev\n",
    "    |> List.map (String.concat \" \")\n",
    "    |> List.map (fun s -> s.Split \" \" |> List.ofArray)\n",
    "\n",
    "type Passport = { byr: string ; iyr : string ; eyr : string ; hgt : string; hcl : string ; ecl : string ; pid : string ; cid : string option }\n",
    "\n",
    "let parseMapping (strings : string list) =\n",
    "    List.map (fun (s : string) -> s.Split \":\") strings\n",
    "    |> List.map (fun a -> (Array.get a 0, Array.get a 1))\n",
    "    |> Map.ofList\n",
    "\n",
    "let mappingToPassport (m : Map<string, string>) : Passport option =\n",
    "    match ( Map.tryFind \"byr\" m,\n",
    "            Map.tryFind \"iyr\" m,\n",
    "            Map.tryFind \"eyr\" m,\n",
    "            Map.tryFind \"hgt\" m,\n",
    "            Map.tryFind \"hcl\" m,\n",
    "            Map.tryFind \"ecl\" m,\n",
    "            Map.tryFind \"pid\" m,\n",
    "            Map.tryFind \"cid\" m) with\n",
    "    | Some byr, Some iyr, Some eyr, Some hgt, Some ecl, Some hcl, Some pid, cid ->\n",
    "        Some { byr = byr ; iyr = iyr ; eyr = eyr ; hgt = hgt ; hcl = hcl ; ecl = ecl ; pid = pid ; cid = cid }\n",
    "    | _ -> None\n",
    "\n",
    "List.map (parseMapping >> mappingToPassport)  passportStrings\n",
    "|> List.filter Option.isSome\n",
    "|> List.length"
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">250</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "--- Part Two ---\n",
    "The line is moving more quickly now, but you overhear airport security talking about how passports with invalid data are getting through. Better add some data validation, quick!\n",
    "\n",
    "You can continue to ignore the cid field, but each other field has strict rules about what values are valid for automatic validation:\n",
    "\n",
    "byr (Birth Year) - four digits; at least 1920 and at most 2002.\n",
    "iyr (Issue Year) - four digits; at least 2010 and at most 2020.\n",
    "eyr (Expiration Year) - four digits; at least 2020 and at most 2030.\n",
    "hgt (Height) - a number followed by either cm or in:\n",
    "If cm, the number must be at least 150 and at most 193.\n",
    "If in, the number must be at least 59 and at most 76.\n",
    "hcl (Hair Color) - a # followed by exactly six characters 0-9 or a-f.\n",
    "ecl (Eye Color) - exactly one of: amb blu brn gry grn hzl oth.\n",
    "pid (Passport ID) - a nine-digit number, including leading zeroes.\n",
    "cid (Country ID) - ignored, missing or not.\n",
    "Your job is to count the passports where all required fields are both present and valid according to the above rules. Here are some example values:\n",
    "\n",
    "byr valid:   2002\n",
    "byr invalid: 2003\n",
    "\n",
    "hgt valid:   60in\n",
    "hgt valid:   190cm\n",
    "hgt invalid: 190in\n",
    "hgt invalid: 190\n",
    "\n",
    "hcl valid:   #123abc\n",
    "hcl invalid: #123abz\n",
    "hcl invalid: 123abc\n",
    "\n",
    "ecl valid:   brn\n",
    "ecl invalid: wat\n",
    "\n",
    "pid valid:   000000001\n",
    "pid invalid: 0123456789\n",
    "Here are some invalid passports:\n",
    "\n",
    "eyr:1972 cid:100\n",
    "hcl:#18171d ecl:amb hgt:170 pid:186cm iyr:2018 byr:1926\n",
    "\n",
    "iyr:2019\n",
    "hcl:#602927 eyr:1967 hgt:170cm\n",
    "ecl:grn pid:012533040 byr:1946\n",
    "\n",
    "hcl:dab227 iyr:2012\n",
    "ecl:brn hgt:182cm pid:021572410 eyr:2020 byr:1992 cid:277\n",
    "\n",
    "hgt:59cm ecl:zzz\n",
    "eyr:2038 hcl:74454a iyr:2023\n",
    "pid:3556412378 byr:2007\n",
    "Here are some valid passports:\n",
    "\n",
    "pid:087499704 hgt:74in ecl:grn iyr:2012 eyr:2030 byr:1980\n",
    "hcl:#623a2f\n",
    "\n",
    "eyr:2029 ecl:blu cid:129 byr:1989\n",
    "iyr:2014 pid:896056539 hcl:#a97842 hgt:165cm\n",
    "\n",
    "hcl:#888785\n",
    "hgt:164cm byr:2001 iyr:2015 cid:88\n",
    "pid:545766238 ecl:hzl\n",
    "eyr:2022\n",
    "\n",
    "iyr:2010 hgt:158cm hcl:#b6652a ecl:blu byr:1944 eyr:2021 pid:093154719\n",
    "Count the number of valid passports - those that have all required fields and valid values. Continue to treat cid as optional. In your batch file, how many passports are valid?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "type Valid<'a> =\n",
    "    | Validated of 'a\n",
    "    | Invalid\n",
    "\n",
    "\n",
    "type BirthYear = int\n",
    "\n",
    "let validateBirthYear x =\n",
    "    1920 <= x && x <= 2002\n",
    "\n",
    "type IssueYear = int\n",
    "\n",
    "let validateIssueYear x =\n",
    "    2010 <= x && x <= 2020\n",
    "\n",
    "type ExpirationYear = int\n",
    "\n",
    "let validateExpirationYear x =\n",
    "    2020 <= x && x <= 2030\n",
    "\n",
    "[<Measure>] type cm\n",
    "[<Measure>] type inch\n",
    "\n",
    "type Height =\n",
    "    | Cm of int<cm>\n",
    "    | Inches of int<inch>\n",
    "\n",
    "let parseHeight = function\n",
    "    | (h : string) when h.EndsWith \"cm\" -> h.Substring (0, (h.Length - 2)) |> int |> LanguagePrimitives.Int32WithMeasure<cm> |> Cm |> Some\n",
    "    | (h : string) when h.EndsWith \"in\" -> h.Substring (0, (h.Length - 2)) |> int |> LanguagePrimitives.Int32WithMeasure<inch> |> Inches |> Some\n",
    "    | _ -> None\n",
    "\n",
    "let validateHeight = function\n",
    "    | Height.Cm h -> 150<cm> <= h && h <= 193<cm>\n",
    "    | Height.Inches h -> 59<inch> <= h && h <= 76<inch>\n",
    "\n",
    "type HairColor = string\n",
    "\n",
    "let charIsHex c = \n",
    "    (c >= '0' && c <= '9') || (c >= 'a' && c <= 'f')\n",
    "\n",
    "\n",
    "let validateHaircolor = function\n",
    "    | '#' :: rest -> Seq.length rest = 6 && Seq.forall charIsHex rest\n",
    "    | _ -> false\n",
    "\n",
    "type EyeColor =\n",
    "    | Amb\n",
    "    | Blu\n",
    "    | Brn\n",
    "    | Gry\n",
    "    | Grn\n",
    "    | Hzl\n",
    "    | Oth\n",
    "\n",
    "let parseEyeColor (s : string) = \n",
    "    match s with\n",
    "    | \"amb\" -> Validated Amb\n",
    "    | \"blu\" -> Validated Blu\n",
    "    | \"brn\" -> Validated Brn\n",
    "    | \"gry\" -> Validated Gry\n",
    "    | \"grn\" -> Validated Grn\n",
    "    | \"hzl\" -> Validated Hzl\n",
    "    | \"oth\" -> Validated Oth\n",
    "    | s -> Invalid\n",
    "\n",
    "type PassportId = string\n",
    "\n",
    "let validatePassportId s =\n",
    "    Seq.length s = 9 && Seq.forall (fun c -> c >= '0' && c <= '9') s\n",
    "\n",
    "type CountryId = string\n",
    "\n",
    "type ValidPassport = { byr: BirthYear ; iyr : IssueYear ; eyr : ExpirationYear ; hgt : Height; hcl : HairColor ; ecl : EyeColor ; pid : PassportId; cid : CountryId option }\n",
    "\n",
    "let validate (f: 'a -> bool ) (a: 'a) : Valid<'a> =\n",
    "    if f a then\n",
    "        Validated a\n",
    "    else\n",
    "        Invalid\n",
    "\n",
    "let parseAndValidate (map: Map<string, string>) =\n",
    "    match ( Map.tryFind \"byr\" map |> Option.map (int >> validate validateBirthYear),\n",
    "            Map.tryFind \"iyr\" map |> Option.map (int >> validate validateIssueYear),\n",
    "            Map.tryFind \"eyr\" map |> Option.map (int >> validate validateExpirationYear),\n",
    "            Map.tryFind \"hgt\" map |> Option.map (parseHeight >> Option.map (validate validateHeight)) |> Option.flatten,\n",
    "            Map.tryFind \"hcl\" map |> Option.map (Seq.toList >> validate validateHaircolor),\n",
    "            Map.tryFind \"ecl\" map |> Option.map (parseEyeColor),\n",
    "            Map.tryFind \"pid\" map |> Option.map (validate validatePassportId),\n",
    "            Map.tryFind \"cid\" map) with\n",
    "    | Some (Validated byr),\n",
    "      Some (Validated iyr),\n",
    "      Some (Validated eyr),\n",
    "      Some (Validated hgt),\n",
    "      Some (Validated hcl),\n",
    "      Some (Validated ecl),\n",
    "      Some (Validated pid),\n",
    "      cid -> Validated { byr = byr ; iyr = iyr ; eyr = eyr ; hgt = hgt ; hcl = ( Array.ofList hcl |> System.String) ; ecl = ecl ; pid = pid; cid = cid}\n",
    "\n",
    "    | _ -> Invalid\n",
    "\n",
    "\n",
    "List.map (parseMapping >> parseAndValidate) passportStrings\n",
    "|> List.where (function\n",
    "    | Validated _ -> true\n",
    "    | Invalid -> false)\n",
    "|> List.length"
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">158</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "file_extension": ".cs",
   "mimetype": "text/x-csharp",
   "name": "C#",
   "pygments_lexer": "csharp",
   "version": "8.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
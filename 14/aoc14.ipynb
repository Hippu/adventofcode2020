{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Day 14: Docking Data ---\n",
    "As your ferry approaches the sea port, the captain asks for your help again. The computer system that runs this port isn't compatible with the docking program on the ferry, so the docking parameters aren't being correctly initialized in the docking program's memory.\n",
    "\n",
    "After a brief inspection, you discover that the sea port's computer system uses a strange bitmask system in its initialization program. Although you don't have the correct decoder chip handy, you can emulate it in software!\n",
    "\n",
    "The initialization program (your puzzle input) can either update the bitmask or write a value to memory. Values and memory addresses are both 36-bit unsigned integers. For example, ignoring bitmasks for a moment, a line like mem[8] = 11 would write the value 11 to memory address 8.\n",
    "\n",
    "The bitmask is always given as a string of 36 bits, written with the most significant bit (representing 2^35) on the left and the least significant bit (2^0, that is, the 1s bit) on the right. The current bitmask is applied to values immediately before they are written to memory: a 0 or 1 overwrites the corresponding bit in the value, while an X leaves the bit in the value unchanged.\n",
    "\n",
    "For example, consider the following program:\n",
    "\n",
    "mask = XXXXXXXXXXXXXXXXXXXXXXXXXXXXX1XXXX0X\n",
    "mem[8] = 11\n",
    "mem[7] = 101\n",
    "mem[8] = 0\n",
    "This program starts by specifying a bitmask (mask = ....). The mask it specifies will overwrite two bits in every written value: the 2s bit is overwritten with 0, and the 64s bit is overwritten with 1.\n",
    "\n",
    "The program then attempts to write the value 11 to memory address 8. By expanding everything out to individual bits, the mask is applied as follows:\n",
    "\n",
    "value:  000000000000000000000000000000001011  (decimal 11)\n",
    "mask:   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX1XXXX0X\n",
    "result: 000000000000000000000000000001001001  (decimal 73)\n",
    "So, because of the mask, the value 73 is written to memory address 8 instead. Then, the program tries to write 101 to address 7:\n",
    "\n",
    "value:  000000000000000000000000000001100101  (decimal 101)\n",
    "mask:   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX1XXXX0X\n",
    "result: 000000000000000000000000000001100101  (decimal 101)\n",
    "This time, the mask has no effect, as the bits it overwrote were already the values the mask tried to set. Finally, the program tries to write 0 to address 8:\n",
    "\n",
    "value:  000000000000000000000000000000000000  (decimal 0)\n",
    "mask:   XXXXXXXXXXXXXXXXXXXXXXXXXXXXX1XXXX0X\n",
    "result: 000000000000000000000000000001000000  (decimal 64)\n",
    "64 is written to address 8 instead, overwriting the value that was there previously.\n",
    "\n",
    "To initialize your ferry's docking program, you need the sum of all values left in memory after the initialization program completes. (The entire 36-bit address space begins initialized to the value 0 at every address.) In the above example, only two values in memory are not zero - 101 (at address 7) and 64 (at address 8) - producing a sum of 165.\n",
    "\n",
    "Execute the initialization program. What is the sum of all values left in memory after it completes? (Do not truncate the sum to 36 bits.)\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "#r \"nuget: FParsec\"\n",
    "open FParsec\n",
    "\n",
    "type b36 = bool array\n",
    "\n",
    "type Mask = (bool option) array\n",
    "\n",
    "type State = { mask: Mask ; memory : Map<int, b36> }\n",
    "\n",
    "type Instruction =\n",
    "    | SetMask of Mask\n",
    "    | WriteValueToAddress of {| value : int64 ; address : int32 |}\n",
    "\n",
    "let int64tob36 (orig: int64) =\n",
    "    Array.init 36 (fun ix -> (orig &&& (pown 2L ix)) = (pown 2L ix))\n",
    "\n",
    "let b36toint64 (orig: b36) =\n",
    "    Array.mapi (fun ix isSet ->\n",
    "        if isSet then\n",
    "            pown 2L ix\n",
    "        else\n",
    "            0L \n",
    "    ) orig |> Array.sum\n",
    "\n",
    "let b36withMask (value: b36) (mask: Mask) =\n",
    "    Array.map2 (fun a b -> \n",
    "        match b with\n",
    "        | Some x -> x\n",
    "        | None -> a\n",
    "        ) value mask\n",
    "\n",
    "\n",
    "let pMask : Parser<Mask, unit> = \n",
    "    many (choice [pchar '0' ; pchar '1' ; pchar 'X' ] |>> \n",
    "        function \n",
    "        | '0' -> Some false\n",
    "        | '1' -> Some true\n",
    "        | 'X' -> None\n",
    "        | _ -> failwith \"Unknown input when parsing mask\"\n",
    "    ) |>> List.rev |>> Array.ofList\n",
    "\n",
    "let parseMaskInstruction : Parser<Instruction, unit> =\n",
    "    pstring \"mask = \" >>. pMask |>> SetMask\n",
    "\n",
    "let parseWrite : Parser<Instruction, unit> =\n",
    "    pipe2\n",
    "        (pstring \"mem\" >>. between (pchar '[') (pchar ']') pint32)\n",
    "        (pstring \" = \" >>. pint64)\n",
    "        (fun address value -> WriteValueToAddress {| value = value ; address = address |})\n",
    "\n",
    "let parseInstruction = parseMaskInstruction <|> parseWrite\n",
    "\n",
    "let inputs = System.IO.File.ReadAllLines \"./input.txt\" |> List.ofArray\n",
    "\n",
    "let instructions = \n",
    "    List.map (run parseInstruction) inputs \n",
    "    |> List.choose (\n",
    "        function\n",
    "        | Success (x, _, _) -> Some x\n",
    "        | Failure _ -> None\n",
    "        )\n",
    "\n",
    "let writeToMemory (memory: Map<int, b36>) (address: int32) (value: int64) (mask: Mask) =\n",
    "    Map.add address (b36withMask (int64tob36 value) mask) memory\n",
    "\n",
    "let runInstruction (state: State) (ins: Instruction) =\n",
    "    match ins with\n",
    "    | SetMask mask -> { state with mask = mask }\n",
    "    | WriteValueToAddress a -> { state with memory = writeToMemory state.memory a.address a.value state.mask }\n",
    "\n",
    "let finalState = List.fold runInstruction { mask = Array.init 36 (fun _ -> None) ; memory = Map.empty } instructions\n",
    "\n",
    "finalState.memory\n",
    "    |> Map.map (fun _ v -> b36toint64 v)\n",
    "    |> Map.fold (fun state k v -> state + v) 0L"
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">15919415426101</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Part Two ---\n",
    "For some reason, the sea port's computer system still can't communicate with your ferry's docking program. It must be using version 2 of the decoder chip!\n",
    "\n",
    "A version 2 decoder chip doesn't modify the values being written at all. Instead, it acts as a memory address decoder. Immediately before a value is written to memory, each bit in the bitmask modifies the corresponding bit of the destination memory address in the following way:\n",
    "\n",
    "If the bitmask bit is 0, the corresponding memory address bit is unchanged.\n",
    "If the bitmask bit is 1, the corresponding memory address bit is overwritten with 1.\n",
    "If the bitmask bit is X, the corresponding memory address bit is floating.\n",
    "A floating bit is not connected to anything and instead fluctuates unpredictably. In practice, this means the floating bits will take on all possible values, potentially causing many memory addresses to be written all at once!\n",
    "\n",
    "For example, consider the following program:\n",
    "\n",
    "mask = 000000000000000000000000000000X1001X\n",
    "mem[42] = 100\n",
    "mask = 00000000000000000000000000000000X0XX\n",
    "mem[26] = 1\n",
    "When this program goes to write to memory address 42, it first applies the bitmask:\n",
    "\n",
    "address: 000000000000000000000000000000101010  (decimal 42)\n",
    "mask:    000000000000000000000000000000X1001X\n",
    "result:  000000000000000000000000000000X1101X\n",
    "After applying the mask, four bits are overwritten, three of which are different, and two of which are floating. Floating bits take on every possible combination of values; with two floating bits, four actual memory addresses are written:\n",
    "\n",
    "000000000000000000000000000000011010  (decimal 26)\n",
    "000000000000000000000000000000011011  (decimal 27)\n",
    "000000000000000000000000000000111010  (decimal 58)\n",
    "000000000000000000000000000000111011  (decimal 59)\n",
    "Next, the program is about to write to memory address 26 with a different bitmask:\n",
    "\n",
    "address: 000000000000000000000000000000011010  (decimal 26)\n",
    "mask:    00000000000000000000000000000000X0XX\n",
    "result:  00000000000000000000000000000001X0XX\n",
    "This results in an address with three floating bits, causing writes to eight memory addresses:\n",
    "\n",
    "000000000000000000000000000000010000  (decimal 16)\n",
    "000000000000000000000000000000010001  (decimal 17)\n",
    "000000000000000000000000000000010010  (decimal 18)\n",
    "000000000000000000000000000000010011  (decimal 19)\n",
    "000000000000000000000000000000011000  (decimal 24)\n",
    "000000000000000000000000000000011001  (decimal 25)\n",
    "000000000000000000000000000000011010  (decimal 26)\n",
    "000000000000000000000000000000011011  (decimal 27)\n",
    "The entire 36-bit address space still begins initialized to the value 0 at every address, and you still need the sum of all values left in memory at the end of the program. In this example, the sum is 208.\n",
    "\n",
    "Execute the initialization program using an emulator for a version 2 decoder chip. What is the sum of all values left in memory after it completes?\n",
    "```\n",
    ""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "type State2 = { memory : Map<int64, int64> ; mask : Mask }\n",
    "\n",
    "let rec decodeMemoryAddress (decoded: bool list) (remaining: (bool option) list) : b36 list =\n",
    "    match remaining with\n",
    "    | [] -> [(List.rev decoded |> Array.ofList)]\n",
    "    | (Some b) :: rem -> decodeMemoryAddress (b :: decoded) rem\n",
    "    | None :: rem -> List.concat [ decodeMemoryAddress (true :: decoded) rem ; decodeMemoryAddress (false :: decoded) rem ]\n",
    "\n",
    "let maskAddress (address: b36) (mask: Mask) =\n",
    "    Array.map2 (fun a b ->\n",
    "        match b with\n",
    "        | Some false -> Some a\n",
    "        | Some true -> Some true\n",
    "        | None -> None\n",
    "    ) address mask\n",
    "\n",
    "let writeValueToAddresses (memory: Map<int64, int64>) (addresses : b36 list) (value: int64) =\n",
    "    List.fold (fun state a -> Map.add (b36toint64 a) value state) memory addresses\n",
    "\n",
    "let runInstruction2 (state: State2) (instruction: Instruction) =\n",
    "    match instruction with \n",
    "    | SetMask m -> { state with mask = m }\n",
    "    | WriteValueToAddress i ->\n",
    "        let maskedAddress = maskAddress (int64 i.address |> int64tob36) state.mask\n",
    "        let decodedAddresses = decodeMemoryAddress [] (List.ofArray maskedAddress)\n",
    "        { state with memory = writeValueToAddresses state.memory decodedAddresses i.value }\n",
    "\n",
    "let finalState2 = List.fold (fun a -> runInstruction2 a) { memory = Map.empty ; mask = Array.init 36 (fun _ -> Some false) } instructions\n",
    "\n",
    "finalState2.memory\n",
    "    |> Map.fold (fun state k v -> state + v) 0L\n",
    ""
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">3443997590975</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "file_extension": ".cs",
   "mimetype": "text/x-csharp",
   "name": "C#",
   "pygments_lexer": "csharp",
   "version": "8.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
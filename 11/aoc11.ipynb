{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Day 11: Seating System ---\n",
    "Your plane lands with plenty of time to spare. The final leg of your journey is a ferry that goes directly to the tropical island where you can finally start your vacation. As you reach the waiting area to board the ferry, you realize you're so early, nobody else has even arrived yet!\n",
    "\n",
    "By modeling the process people use to choose (or abandon) their seat in the waiting area, you're pretty sure you can predict the best place to sit. You make a quick map of the seat layout (your puzzle input).\n",
    "\n",
    "The seat layout fits neatly on a grid. Each position is either floor (.), an empty seat (L), or an occupied seat (#). For example, the initial seat layout might look like this:\n",
    "\n",
    "L.LL.LL.LL\n",
    "LLLLLLL.LL\n",
    "L.L.L..L..\n",
    "LLLL.LL.LL\n",
    "L.LL.LL.LL\n",
    "L.LLLLL.LL\n",
    "..L.L.....\n",
    "LLLLLLLLLL\n",
    "L.LLLLLL.L\n",
    "L.LLLLL.LL\n",
    "Now, you just need to model the people who will be arriving shortly. Fortunately, people are entirely predictable and always follow a simple set of rules. All decisions are based on the number of occupied seats adjacent to a given seat (one of the eight positions immediately up, down, left, right, or diagonal from the seat). The following rules are applied to every seat simultaneously:\n",
    "\n",
    "If a seat is empty (L) and there are no occupied seats adjacent to it, the seat becomes occupied.\n",
    "If a seat is occupied (#) and four or more seats adjacent to it are also occupied, the seat becomes empty.\n",
    "Otherwise, the seat's state does not change.\n",
    "Floor (.) never changes; seats don't move, and nobody sits on the floor.\n",
    "\n",
    "After one round of these rules, every seat in the example layout becomes occupied:\n",
    "\n",
    "#.##.##.##\n",
    "#######.##\n",
    "#.#.#..#..\n",
    "####.##.##\n",
    "#.##.##.##\n",
    "#.#####.##\n",
    "..#.#.....\n",
    "##########\n",
    "#.######.#\n",
    "#.#####.##\n",
    "After a second round, the seats with four or more occupied adjacent seats become empty again:\n",
    "\n",
    "#.LL.L#.##\n",
    "#LLLLLL.L#\n",
    "L.L.L..L..\n",
    "#LLL.LL.L#\n",
    "#.LL.LL.LL\n",
    "#.LLLL#.##\n",
    "..L.L.....\n",
    "#LLLLLLLL#\n",
    "#.LLLLLL.L\n",
    "#.#LLLL.##\n",
    "This process continues for three more rounds:\n",
    "\n",
    "#.##.L#.##\n",
    "#L###LL.L#\n",
    "L.#.#..#..\n",
    "#L##.##.L#\n",
    "#.##.LL.LL\n",
    "#.###L#.##\n",
    "..#.#.....\n",
    "#L######L#\n",
    "#.LL###L.L\n",
    "#.#L###.##\n",
    "#.#L.L#.##\n",
    "#LLL#LL.L#\n",
    "L.L.L..#..\n",
    "#LLL.##.L#\n",
    "#.LL.LL.LL\n",
    "#.LL#L#.##\n",
    "..L.L.....\n",
    "#L#LLLL#L#\n",
    "#.LLLLLL.L\n",
    "#.#L#L#.##\n",
    "#.#L.L#.##\n",
    "#LLL#LL.L#\n",
    "L.#.L..#..\n",
    "#L##.##.L#\n",
    "#.#L.LL.LL\n",
    "#.#L#L#.##\n",
    "..L.L.....\n",
    "#L#L##L#L#\n",
    "#.LLLLLL.L\n",
    "#.#L#L#.##\n",
    "At this point, something interesting happens: the chaos stabilizes and further applications of these rules cause no seats to change state! Once people stop moving around, you count 37 occupied seats.\n",
    "\n",
    "Simulate your seating area by applying the seating rules repeatedly until no seats change state. How many seats end up occupied?\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "let input = System.IO.File.ReadAllLines \"./input.txt\"\n",
    "\n",
    "type Cell =\n",
    "    | EmptySeat\n",
    "    | OccupiedSeat\n",
    "    | Floor\n",
    "\n",
    "let parseCell = function\n",
    "    | 'L' -> EmptySeat\n",
    "    | '#' -> OccupiedSeat\n",
    "    | '.' -> Floor\n",
    "    | _ -> failwith \"Unexpected input\"\n",
    "\n",
    "let initialGrid = Array.map (Seq.map parseCell) input |> array2D\n",
    "\n",
    "let neighbors row col grid =\n",
    "    [ (row - 1, col - 1); (row - 1, col); (row - 1, col + 1);\n",
    "      (row, col - 1); (row, col + 1);\n",
    "      (row + 1, col - 1); (row + 1, col); (row + 1, col + 1);\n",
    "    ] \n",
    "    |> List.filter (fun (row, col) -> \n",
    "        row >= 0 && row < Array2D.length1 grid &&\n",
    "        col >= 0 && col < Array2D.length2 grid )\n",
    "    |> List.map (fun (row, col) -> Array2D.get grid row col )\n",
    "\n",
    "let occupiedSeatsNearby row col grid =\n",
    "    neighbors row col grid\n",
    "    |> List.sumBy (function\n",
    "        | OccupiedSeat -> 1\n",
    "        | _ -> 0\n",
    "    )\n",
    "\n",
    "let nextState row col (cell: Cell) (grid: Cell[,]) =\n",
    "    match cell with\n",
    "    | EmptySeat when (occupiedSeatsNearby row col grid = 0)  -> OccupiedSeat\n",
    "    | OccupiedSeat when (occupiedSeatsNearby row col grid >= 4) -> EmptySeat\n",
    "    | x -> x\n",
    " \n",
    "\n",
    "let runTick grid =\n",
    "    Array2D.mapi (fun row col cell -> nextState row col cell grid) grid\n",
    "\n",
    "let gridIsEqual (grid1: Cell[,]) (grid2: Cell[,]) =\n",
    "    seq { for row in 0 .. (Array2D.length1 grid1 - 1) do\n",
    "            for col in 0 .. (Array2D.length2 grid1 - 1) do\n",
    "                yield (grid1.[row, col] = grid2.[row, col]) }\n",
    "    |> Seq.reduce (&&)\n",
    "\n",
    "let rec runUntilStable (state: Cell[,]) =\n",
    "    match runTick state with\n",
    "    | newState when gridIsEqual state newState -> newState\n",
    "    | newState -> runUntilStable newState\n",
    "\n",
    "let countOccupied grid =\n",
    "    let mutable count = 0\n",
    "    for x in 0 .. (Array2D.length1 grid - 1) do\n",
    "        for y in 0 .. (Array2D.length2 grid - 1) do\n",
    "            match grid.[x, y] with\n",
    "            | OccupiedSeat -> (count <- count + 1)\n",
    "            | _ -> ()\n",
    "    count\n",
    "\n",
    "runUntilStable initialGrid |> countOccupied\n",
    "\n",
    ""
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">2418</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Part Two ---\n",
    "As soon as people start to arrive, you realize your mistake. People don't just care about adjacent seats - they care about the first seat they can see in each of those eight directions!\n",
    "\n",
    "Now, instead of considering just the eight immediately adjacent seats, consider the first seat in each of those eight directions. For example, the empty seat below would see eight occupied seats:\n",
    "\n",
    ".......#.\n",
    "...#.....\n",
    ".#.......\n",
    ".........\n",
    "..#L....#\n",
    "....#....\n",
    ".........\n",
    "#........\n",
    "...#.....\n",
    "The leftmost empty seat below would only see one empty seat, but cannot see any of the occupied ones:\n",
    "\n",
    ".............\n",
    ".L.L.#.#.#.#.\n",
    ".............\n",
    "The empty seat below would see no occupied seats:\n",
    "\n",
    ".##.##.\n",
    "#.#.#.#\n",
    "##...##\n",
    "...L...\n",
    "##...##\n",
    "#.#.#.#\n",
    ".##.##.\n",
    "Also, people seem to be more tolerant than you expected: it now takes five or more visible occupied seats for an occupied seat to become empty (rather than four or more from the previous rules). The other rules still apply: empty seats that see no occupied seats become occupied, seats matching no rule don't change, and floor never changes.\n",
    "\n",
    "Given the same starting layout as above, these new rules cause the seating area to shift around as follows:\n",
    "\n",
    "L.LL.LL.LL\n",
    "LLLLLLL.LL\n",
    "L.L.L..L..\n",
    "LLLL.LL.LL\n",
    "L.LL.LL.LL\n",
    "L.LLLLL.LL\n",
    "..L.L.....\n",
    "LLLLLLLLLL\n",
    "L.LLLLLL.L\n",
    "L.LLLLL.LL\n",
    "#.##.##.##\n",
    "#######.##\n",
    "#.#.#..#..\n",
    "####.##.##\n",
    "#.##.##.##\n",
    "#.#####.##\n",
    "..#.#.....\n",
    "##########\n",
    "#.######.#\n",
    "#.#####.##\n",
    "#.LL.LL.L#\n",
    "#LLLLLL.LL\n",
    "L.L.L..L..\n",
    "LLLL.LL.LL\n",
    "L.LL.LL.LL\n",
    "L.LLLLL.LL\n",
    "..L.L.....\n",
    "LLLLLLLLL#\n",
    "#.LLLLLL.L\n",
    "#.LLLLL.L#\n",
    "#.L#.##.L#\n",
    "#L#####.LL\n",
    "L.#.#..#..\n",
    "##L#.##.##\n",
    "#.##.#L.##\n",
    "#.#####.#L\n",
    "..#.#.....\n",
    "LLL####LL#\n",
    "#.L#####.L\n",
    "#.L####.L#\n",
    "#.L#.L#.L#\n",
    "#LLLLLL.LL\n",
    "L.L.L..#..\n",
    "##LL.LL.L#\n",
    "L.LL.LL.L#\n",
    "#.LLLLL.LL\n",
    "..L.L.....\n",
    "LLLLLLLLL#\n",
    "#.LLLLL#.L\n",
    "#.L#LL#.L#\n",
    "#.L#.L#.L#\n",
    "#LLLLLL.LL\n",
    "L.L.L..#..\n",
    "##L#.#L.L#\n",
    "L.L#.#L.L#\n",
    "#.L####.LL\n",
    "..#.#.....\n",
    "LLL###LLL#\n",
    "#.LLLLL#.L\n",
    "#.L#LL#.L#\n",
    "#.L#.L#.L#\n",
    "#LLLLLL.LL\n",
    "L.L.L..#..\n",
    "##L#.#L.L#\n",
    "L.L#.LL.L#\n",
    "#.LLLL#.LL\n",
    "..#.L.....\n",
    "LLL###LLL#\n",
    "#.LLLLL#.L\n",
    "#.L#LL#.L#\n",
    "Again, at this point, people stop shifting around and the seating area reaches equilibrium. Once this occurs, you count 26 occupied seats.\n",
    "\n",
    "Given the new visibility method and the rule change for occupied seats becoming empty, once equilibrium is reached, how many seats end up occupied?\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "let firstSeatInEachDirection row col grid =\n",
    "    let directions = [\n",
    "        (-1, -1); (-1, 0); (-1, 1);\n",
    "        (0, -1); (0, 1);\n",
    "        (1, -1); (1, 0); (1, 1)\n",
    "    ]\n",
    "\n",
    "    let rec firstSeatInDirection row col (direction: (int * int)) =\n",
    "        let rowd, cold = direction\n",
    "        if row + rowd >= (Array2D.length1 grid) || row + rowd < 0 || col + cold >= (Array2D.length2 grid) || col + cold < 0 then\n",
    "            Floor\n",
    "        else\n",
    "            match Array2D.get grid (row + rowd) (col + cold) with\n",
    "            | OccupiedSeat -> OccupiedSeat\n",
    "            | EmptySeat -> EmptySeat\n",
    "            | Floor -> firstSeatInDirection (row + rowd) (col + cold) direction\n",
    "\n",
    "    List.map (firstSeatInDirection row col) directions\n",
    "\n",
    "let countOccupiedSeats =\n",
    "    List.sumBy (function\n",
    "    | OccupiedSeat -> 1\n",
    "    | _ -> 0)\n",
    "\n",
    "let nextState2 row col cell grid =\n",
    "    match cell with\n",
    "    | OccupiedSeat when (firstSeatInEachDirection row col grid |> countOccupiedSeats) >= 5 -> EmptySeat\n",
    "    | EmptySeat when (firstSeatInEachDirection row col grid |> countOccupiedSeats) = 0 -> OccupiedSeat\n",
    "    | x -> x\n",
    "\n",
    "let runTick2 grid =\n",
    "    Array2D.mapi (fun row col cell -> nextState2 row col cell grid) grid\n",
    "    \n",
    "let rec runUntilStable2 state =\n",
    "    match runTick2 state with\n",
    "    | newState when gridIsEqual state newState -> newState\n",
    "    | newState -> runUntilStable2 newState\n",
    "\n",
    "runUntilStable2 initialGrid |> countOccupied\n",
    ""
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">2144</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "file_extension": ".cs",
   "mimetype": "text/x-csharp",
   "name": "C#",
   "pygments_lexer": "csharp",
   "version": "8.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
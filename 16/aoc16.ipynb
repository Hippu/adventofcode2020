{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Day 16: Ticket Translation ---\n",
    "As you're walking to yet another connecting flight, you realize that one of the legs of your re-routed trip coming up is on a high-speed train. However, the train ticket you were given is in a language you don't understand. You should probably figure out what it says before you get to the train station after the next flight.\n",
    "\n",
    "Unfortunately, you can't actually read the words on the ticket. You can, however, read the numbers, and so you figure out the fields these tickets must have and the valid ranges for values in those fields.\n",
    "\n",
    "You collect the rules for ticket fields, the numbers on your ticket, and the numbers on other nearby tickets for the same train service (via the airport security cameras) together into a single document you can reference (your puzzle input).\n",
    "\n",
    "The rules for ticket fields specify a list of fields that exist somewhere on the ticket and the valid ranges of values for each field. For example, a rule like class: 1-3 or 5-7 means that one of the fields in every ticket is named class and can be any value in the ranges 1-3 or 5-7 (inclusive, such that 3 and 5 are both valid in this field, but 4 is not).\n",
    "\n",
    "Each ticket is represented by a single line of comma-separated values. The values are the numbers on the ticket in the order they appear; every ticket has the same format. For example, consider this ticket:\n",
    "\n",
    ".--------------------------------------------------------.\n",
    "| ????: 101    ?????: 102   ??????????: 103     ???: 104 |\n",
    "|                                                        |\n",
    "| ??: 301  ??: 302             ???????: 303      ??????? |\n",
    "| ??: 401  ??: 402           ???? ????: 403    ????????? |\n",
    "'--------------------------------------------------------'\n",
    "Here, ? represents text in a language you don't understand. This ticket might be represented as 101,102,103,104,301,302,303,401,402,403; of course, the actual train tickets you're looking at are much more complicated. In any case, you've extracted just the numbers in such a way that the first number is always the same specific field, the second number is always a different specific field, and so on - you just don't know what each position actually means!\n",
    "\n",
    "Start by determining which tickets are completely invalid; these are tickets that contain values which aren't valid for any field. Ignore your ticket for now.\n",
    "\n",
    "For example, suppose you have the following notes:\n",
    "\n",
    "class: 1-3 or 5-7\n",
    "row: 6-11 or 33-44\n",
    "seat: 13-40 or 45-50\n",
    "\n",
    "your ticket:\n",
    "7,1,14\n",
    "\n",
    "nearby tickets:\n",
    "7,3,47\n",
    "40,4,50\n",
    "55,2,20\n",
    "38,6,12\n",
    "It doesn't matter which position corresponds to which field; you can identify invalid nearby tickets by considering only whether tickets contain values that are not valid for any field. In this example, the values on the first nearby ticket are all valid for at least one field. This is not true of the other three nearby tickets: the values 4, 55, and 12 are are not valid for any field. Adding together all of the invalid values produces your ticket scanning error rate: 4 + 55 + 12 = 71.\n",
    "\n",
    "Consider the validity of the nearby tickets you scanned. What is your ticket scanning error rate?\n",
    "\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "#r \"nuget: FParsec\"\n",
    "open FParsec\n",
    "\n",
    "type FieldRule = { field: string ; range1 : int * int ; range2 : int * int }\n",
    "\n",
    "let input = System.IO.File.ReadAllLines \"./input.txt\"\n",
    "\n",
    "let ruleLines = Array.sub input 0 20 |> List.ofArray\n",
    "let myTicket = Array.get input 22\n",
    "let nearbyTicketLines = Array.sub input 25 (Array.length input - 25) |> List.ofArray\n",
    "\n",
    "let parseRange : Parser<int * int, unit> =\n",
    "    pipe2\n",
    "        (pint32 .>> pchar '-')\n",
    "        (pint32)\n",
    "        (fun a b -> (a, b))\n",
    "\n",
    "let parseRule : Parser<FieldRule, unit> =\n",
    "    pipe3\n",
    "        (charsTillString \": \" true 100)\n",
    "        (parseRange .>> pstring \" or \")\n",
    "        (parseRange)\n",
    "        (fun field r1 r2 -> { field = field ; range1 = r1 ; range2 = r2})\n",
    "\n",
    "let parseTicket :Parser<int list, unit> =\n",
    "    sepBy pint32 (pchar ',')    \n",
    "\n",
    "let rules = \n",
    "    List.map (run parseRule) ruleLines \n",
    "    |> List.choose (function\n",
    "    | Success (a, _, _) -> Some a\n",
    "    | _ -> None\n",
    ")\n",
    "\n",
    "let nearbyTickets = \n",
    "    List.map (run parseTicket) nearbyTicketLines |>\n",
    "    List.choose (function\n",
    "    | Success (a, _, _) -> Some a\n",
    "    | _ -> None\n",
    ")\n",
    "\n",
    "let validForRule (value: int) (rule: FieldRule) =\n",
    "    let (r1_min, r1_max) = rule.range1\n",
    "    let (r2_min, r2_max) = rule.range2\n",
    "\n",
    "    (r1_min <= value && value <= r1_max) || (r2_min <= value && value <= r2_max)\n",
    "\n",
    "let validForAnyRule (value: int) (rules: FieldRule list) =\n",
    "    List.exists (fun r -> validForRule value r ) rules\n",
    "\n",
    "List.map (List.filter (fun v -> validForAnyRule v rules |> not)) nearbyTickets\n",
    "|> List.sumBy (List.sum)\n",
    "\n",
    "\n",
    "\n",
    "    "
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">21956</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "```\n",
    "--- Part Two ---\n",
    "Now that you've identified which tickets contain invalid values, discard those tickets entirely. Use the remaining valid tickets to determine which field is which.\n",
    "\n",
    "Using the valid ranges for each field, determine what order the fields appear on the tickets. The order is consistent between all tickets: if seat is the third field, it is the third field on every ticket, including your ticket.\n",
    "\n",
    "For example, suppose you have the following notes:\n",
    "\n",
    "class: 0-1 or 4-19\n",
    "row: 0-5 or 8-19\n",
    "seat: 0-13 or 16-19\n",
    "\n",
    "your ticket:\n",
    "11,12,13\n",
    "\n",
    "nearby tickets:\n",
    "3,9,18\n",
    "15,1,5\n",
    "5,14,9\n",
    "Based on the nearby tickets in the above example, the first position must be row, the second position must be class, and the third position must be seat; you can conclude that in your ticket, class is 12, row is 11, and seat is 13.\n",
    "\n",
    "Once you work out which field is which, look for the six fields on your ticket that start with the word departure. What do you get if you multiply those six values together?\n",
    "\n",
    "```"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "source": [
    "#!fsharp\n",
    "let validNearbyTickets = \n",
    "    List.filter (List.forall (fun v -> validForAnyRule v rules)) nearbyTickets\n",
    "\n",
    "let ticketColumns = List.transpose validNearbyTickets\n",
    "\n",
    "let rec validForRules (validFor: FieldRule list) (values: int list) (remaining: FieldRule list) =\n",
    "    match remaining with\n",
    "    | r :: rs -> \n",
    "        if List.forall (fun v -> validForRule v r) values then\n",
    "            validForRules (r :: validFor) values rs\n",
    "        else\n",
    "            validForRules validFor values rs\n",
    "    | [] -> validFor\n",
    "\n",
    "let validRulesForColumns = List.map (fun vs -> validForRules [] vs rules) ticketColumns\n",
    "\n",
    "let rec combinations (path: FieldRule list ) (possibilities : FieldRule list list) : FieldRule list list =\n",
    "    match possibilities with\n",
    "    | (xs) :: remaining -> List.concat (List.map (fun x -> combinations (x :: path) (List.map (List.filter ((<>) x)) remaining) ) xs)\n",
    "    | [] -> [List.rev path]\n",
    "\n",
    "let fields = combinations [] validRulesForColumns\n",
    "\n",
    "let field = List.head fields\n",
    "\n",
    "let departureIndexes = List.indexed field |> List.filter (fun (_, x) -> x.field.StartsWith \"departure\")\n",
    "\n",
    "let myTicketValues = \n",
    "    match run parseTicket myTicket with\n",
    "    | Success (a, _, _) -> a\n",
    "    | Failure _ -> failwith \"Couldn't parse my ticket\"\n",
    "\n",
    "List.indexed myTicketValues\n",
    "|> List.fold \n",
    "    (fun state (ix, a) ->\n",
    "        if (List.exists (fst >> ((=) ix)) departureIndexes) then\n",
    "            state * (int64 a)\n",
    "        else\n",
    "            state\n",
    "    ) 1L\n",
    ""
   ],
   "outputs": [
    {
     "output_type": "execute_result",
     "data": {
      "text/html": "<div class=\"dni-plaintext\">3709435214239</div>"
     },
     "execution_count": 1,
     "metadata": {}
    }
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": ".NET (C#)",
   "language": "C#",
   "name": ".net-csharp"
  },
  "language_info": {
   "file_extension": ".cs",
   "mimetype": "text/x-csharp",
   "name": "C#",
   "pygments_lexer": "csharp",
   "version": "8.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}